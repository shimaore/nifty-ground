    Promise = require 'bluebird'
    fs = Promise.promisifyAll require 'fs'
    path = require 'path'
    spawn = require('child_process').spawn
    pkg = require '../package.json'
    assert = require 'assert'

    trace_dir = path.join process.cwd(), 'pcap'
    ringsize = process.env.RINGSIZE ? 50
    assert process.env.INTERFACES?, 'Missing INTERFACES environment variable'
    interfaces = process.env.INTERFACES.split /\s+/

    seconds = 1000
    minutes = 60*seconds

    debug = (require 'debug') "#{pkg.name}:cleanup"

    module.exports = (new_interfaces) ->
      interfaces = new_interfaces if new_interfaces?
      return unless interfaces?.length > 0

For each interface, list of files.

      fs.readdirAsync trace_dir
      .catch (error) ->
        console.error "#{error} while readdir #{trace_dir}"
        []
      .then (names)  ->
        ind = {}
        for name in names
          do (name) ->
            if r = name.match /^([^_]+)_\d+_(\d+).pcap(.gz)?$/
              [_name,intf,time,comp] = r
              ind[intf] ?= []
              ind[intf].push {name,time,comp}

Also cleanup files generated by mergecap etc.

            if r = name.match /^\.tmp/
              full = path.join trace_dir, name
              fs.lstatAsync full
              .then (stats) ->
                if stats.mtime - new Date() < -30*minutes
                  fs.unlink full
              .catch (error) ->
                console.dir {error, file:full}

        debug ind

        for intf, files of ind

Sort by ascending timestamp, oldest first.

          files = files.sort (a,b) -> if a.time > b.time then 1 else -1

          while files.length > ringsize
            a = files.shift()
            full = path.join trace_dir, a.name
            debug "unlink #{full}"
            fs.unlinkAsync full
            .catch (error) ->
              console.dir {error, file:full}

          files.pop() # remove current file
          for a in files
            do (a) ->
              return if a.comp
              full = path.join trace_dir, a.name
              debug "gzip #{full}"
              spawn '/bin/gzip', [full], stdio:'ignore'
