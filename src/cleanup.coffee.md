    fs = (require 'fs').promises
    path = require 'path'
    spawn = require('child_process').spawn
    assert = require 'assert'

    seconds = 1000
    minutes = 60*seconds

    debug = (require 'tangible') "nifty-ground:cleanup"

    module.exports = (new_interfaces) ->
      trace_dir = process.env.DATA_DIR ? '/data'
      ringsize = process.env.RINGSIZE ? 50
      assert process.env.INTERFACES?, 'Missing INTERFACES environment variable'
      interfaces = process.env.INTERFACES.split /\s+/

      interfaces = new_interfaces if new_interfaces?
      return unless interfaces?.length > 0

For each interface, list of files.

      try
        names = await fs.readdir trace_dir
      catch error
        debug.dev "#{error} while readdir #{trace_dir}"
        names = []

      ind = {}
      for name in names
        await do (name) ->
          if r = name.match /^([^_]+)_\d+_(\d+).pcap(.gz)?$/
            [_name,intf,time,comp] = r
            ind[intf] ?= []
            ind[intf].push {name,time,comp}

Also cleanup files generated by mergecap etc.

          if r = name.match /^\.tmp/
            try
              full = path.join trace_dir, name
              stats = await fs.lstat full
              if stats.mtime - new Date() < -30*minutes
                await fs.unlink full
            catch error
              debug.dev "lstat/unlink: #{error}", full

          return

      debug ind

      for intf, files of ind

Sort by ascending timestamp, oldest first.

        files = files.sort (a,b) -> if a.time > b.time then 1 else -1

        while files.length > ringsize
          a = files.shift()
          try
            full = path.join trace_dir, a.name
            debug "unlink #{full}"
            await fs.unlink full
          catch error
            debug.dev "unlink #{error}", full

        files.pop() # remove current file
        for a in files
          do (a) ->
            return if a.comp
            try
              full = path.join trace_dir, a.name
              debug "gzip #{full}"
              spawn '/bin/gzip', [full], stdio:'ignore'
            catch error
              debug.dev "spawn gzip #{error}", full
            return

      return
