#!/usr/bin/env coffee

fs = require 'fs'
path = require 'path'
spawn = require('child_process').spawn

default_workdir = '/opt/ccnq3/traces'
default_ringsize = 50

debug = false

require('ccnq3_config').get (config) ->

  return unless config.traces?.interfaces?.length > 0

  trace_dir = config.traces.workdir ? default_workdir
  ringsize = config.traces.ringsize ? default_ringsize

  # For each interface, list of files.
  ind = {}
  fs.readdir trace_dir, (err,names)  ->
    for name in names
      if r = name.match /^([^_]+)_\d+_(\d+).pcap(.gz)?$/
        [_name,intf,time,comp] = r
        ind[intf] ?= []
        ind[intf].push {name,time,comp}

    if debug
      console.log JSON.stringify ind, '  '

    for intf, files of ind

      # Sort by ascending timestamp, oldest first.
      files = files.sort (a,b) -> if a.time > b.time then 1 else -1

      while files.length > ringsize
        a = files.shift()
        full = path.join trace_dir, a.name
        if debug
          console.log "unlink #{full}"
        else
          fs.unlink full
      files.pop() # remove current file
      for a in files
        do (a) ->
          return if a.comp
          full = path.join trace_dir, a.name
          if debug
            console.log "gzip #{full}"
          else
            spawn '/bin/gzip', [full], stdio:'ignore'
